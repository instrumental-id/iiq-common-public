publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            if (JavaVersion.current().isJava11Compatible()) {
                artifact tasks.named("javadoc11Jar")
            }

            pom {
                name = 'IIQ Common Public'
                description = 'A library of common SailPoint IdentityIQ utilities for Rules and Java code'
                url = 'https://www.identityworksllc.com/utilities/iiqcommon'
                packaging = "jar"
                inceptionYear = "2022"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = "repo"
                    }
                }

                organization {
                    name = "Instrumental Identity"
                    url = "https://www.instrumentalidentity.com"
                }

                developers {
                    developer {
                        id = "drosenbauer"
                        name = "Devin Rosenbauer"
                        email = "devin@instrumentalid.com"
                        organization = "Instrumental Identity"
                    }
                }

                scm {
                    connection = 'scm:git:git://git.identityworksllc.com/pub/iiqcommon.git'
                    developerConnection = 'scm:git:ssh://git.identityworksllc.com/pub/iiqcommon.git'
                    url = 'https://www.identityworksllc.com/utilities/iiqcommon'
                }
            }

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text().contains('identityiq')
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
    repositories {
        maven {
            url "https://git.identityworksllc.com/api/v4/projects/157/packages/maven"

            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv("CI_JOB_TOKEN")
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
        /*
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/instrumental-id/iiq-common-public"

            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        */
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }

    def signingKey = findProperty("signingKey")
    if (signingKey) {
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.named("mavenJava").get()
}
